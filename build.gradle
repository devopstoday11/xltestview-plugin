/*
 * Copyright (c) 2014-2015, XebiaLabs B.V., All rights reserved.
 *
 * The XL TestView plugin for Jenkins is licensed under the terms of the GPLv2
 * <http://www.gnu.org/licenses/old-licenses/gpl-2.0.html>, like most XebiaLabs
 * Libraries. There are special exceptions to the terms and conditions of the
 * GPLv2 as it is applied to this software, see the FLOSS License Exception
 * <https://github.com/jenkinsci/xltestview-plugin/blob/master/LICENSE>.
 *
 * This program is free software; you can redistribute it and/or modify it
 * under the terms of the GNU General Public License as published by the Free
 * Software Foundation; version 2 of the License.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT
 * ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
 * FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for
 * more details.
 *
 * You should have received a copy of the GNU General Public License along with
 * this program; if not, write to the Free Software Foundation, Inc., 51
 * Franklin St, Fifth Floor, Boston, MA 02110-1301 USA
 */

buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'http://repo.jenkins-ci.org/releases/'
        }
    }
    dependencies {
        classpath 'org.jenkins-ci.tools:gradle-jpi-plugin:0.11.1'
        classpath 'org.eclipse.jgit:org.eclipse.jgit:3.6.1.201501031845-r'
    }
}

apply plugin: 'java'
apply plugin: 'jpi'
apply plugin: com.xebialabs.gradle.plugins.release.ReleasePlugin

apply plugin: 'eclipse'
apply plugin: 'idea'

group = "com.xebialabs.xlt.ci"
description = "Aggregate your test results in &lt;a href='http://www.xebialabs.com'&gt;XebiaLabs XL TestView&lt;/a&gt;."

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

com.xebialabs.gradle.plugins.release.util.VersionUtil.setVersion(project)

if (project.rootProject == project) {
    String msg = "${project.name}-${project.version}"
    println "+".padRight(msg.length() + 3, '-') + "+"
    println "| $msg |"
    println "+".padRight(msg.length() + 3, '-') + "+"
}

build.dependsOn jpi

task deleteGeneratedSrcLocalizer(type:Delete) {
    delete "build/generated-src/localizer"
}

localizer.dependsOn deleteGeneratedSrcLocalizer

jenkinsPlugin {
    coreVersion = '1.609.1' // we need to support LTS versions
    displayName = "XL TestView Plugin"
    url = 'https://wiki.jenkins-ci.org/display/JENKINS/XL+TestView+Plugin'
    gitHubUrl = 'https://github.com/jenkinsci/xltestview-plugin'

    // put the localizer output outside buildDir so the idea plugin does not
    // create a non working configuration. The ignore of the buildDir seems
    // to prevail over the configuration of the sourceDir.
    localizerOutputDir = "${project.projectDir}/src/generated-localizer"

    developers {
        developer {
            id 'XebiaLabsCI'
            name 'XebiaLabs'
            email 'xl-developers (at) xebialabs (dot) com'
        }
    }
}

repositories {
    mavenCentral()
    maven {
        url 'http://repo.jenkins-ci.org/public/'
    }
}

test {
    useTestNG()
}

configurations {
    compile.transitive = true

    // obscure artifact that is referenced somewhere from test configurations, but seems to not exist anymore
    all*.exclude group: 'org.jenkins-ci.main', module: 'ui-samples-plugin'

    // this artifact lives in a netbeans repository which behaves flakey
    // it's not used so exclude it.
    testCompile.exclude group: 'org.netbeans.modules', module: 'org-netbeans-insane'
}

ext {
    jacksonVersion = '2.5.3'
    okHttpVersion = '2.5.0'
    slf4jVersion = '1.7.12'
    logbackVersion = '1.1.3'
}

dependencies {
    compile "org.slf4j:slf4j-api:${slf4jVersion}"

    jenkinsPlugins "org.jenkins-ci.plugins:credentials:1.22@jar"

    compile "com.squareup.okhttp:okhttp:${okHttpVersion}"
    compile "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"

    // shut up various warnings regarding annotations
    compile "com.google.code.findbugs:jsr305:2.0.2"
    compile "findbugs:annotations:1.0.0"

    testCompile "org.slf4j:log4j-over-slf4j:${slf4jVersion}"
    testCompile "org.slf4j:jul-to-slf4j:${slf4jVersion}"
    testCompile "org.slf4j:jcl-over-slf4j:${slf4jVersion}"
    testCompile "ch.qos.logback:logback-classic:${logbackVersion}"
    testCompile "ch.qos.logback:logback-core:${logbackVersion}"

    testCompile "org.testng:testng:6.8.21"
    testCompile "com.squareup.okhttp:mockwebserver:${okHttpVersion}"
    testCompile "org.mockito:mockito-core:1.10.19"

    // also included in jenkins itself
    testCompile "javax.mail:mail:1.4.4"
}

// configure release plugin
import com.xebialabs.gradle.plugins.release.util.ReleaseSupport
import com.xebialabs.gradle.plugins.release.util.BetaReleaseSupport

releasePlugin {
    types {
        release {
            tag         { -> ReleaseSupport.tag(project) }
            preBuild    { task -> ReleaseSupport.preBuildConfiguration(project, task) }
            postBuild   { task -> ReleaseSupport.postBuildConfiguration(project, task) }
        }
        beta {
            tag         { -> BetaReleaseSupport.tag(project) }
            preBuild    { task -> BetaReleaseSupport.preBuildConfiguration(project, task) }
            postBuild   { task -> BetaReleaseSupport.postBuildConfiguration(project, task) }
        }
    }
}
